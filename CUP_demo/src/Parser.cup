package lang.parser;

import java.io.*;
import java.util.*;
import java_cup.runtime.Symbol;
import ast.*;

parser code {:

	Expresion exp;
	
	public void setExpresion(Expresion exp){ this.exp = exp; }
	public Expresion getExpresion(){
		if(exp == null) return new Expresion();
		else return exp; 
	}
		
	public static Lexer makeLexer(String input) throws Exception {
		return new Lexer(new StringReader(input));
	}
	
	public static Parser makeParser(String input) throws Exception {
		return new Parser(makeLexer(input));
	}
	
	public static Object parseString(String input) throws Exception {
		Parser parser = Parser.makeParser(input);
		Symbol result = parser.parse();
		return result.value;
	}
	
:}

// Rules

terminal P1, P2, COLON, COMMA, EOL;
terminal INTERSECCION, UNION,
		 CONCATENACION, DIFERENCIA,
		 FILTRO, ATTRIBUTES, ELEMENTS;
terminal PESOS;
terminal Float num;
terminal String id;
terminal String str;
terminal Boolean bool;
terminal String fil;

non terminal Nodo obj;
non terminal Nodo lista;

non terminal Object valor;

non terminal Expresion root;
non terminal Expresion expresion;

precedence left ELEMENTS, ATTRIBUTES;
precedence left UNION, CONCATENACION,
			DIFERENCIA, INTERSECCION;
precedence left P1, P2;

root ::= expresion:e EOL {: RESULT = e; :}
		| root:r expresion:e EOL {: RESULT = e; :}
		;
obj ::= id:tag P1 lista:l P2 
			{: RESULT = l; RESULT.setTag(tag); :}
		| id:tag P1 P2 {: RESULT = new Nodo(); RESULT.setTag(tag); :}
		;
lista ::= id:i COLON valor:v 
			{: RESULT = new Nodo(); RESULT.addAttr(i,v); :}
		| valor:v {: RESULT = new Nodo(); RESULT.addElem(v); :}
		| lista:l COMMA id:i COLON valor:v
			{: RESULT = l; RESULT.addAttr(i,v); :}
		| lista:l COMMA valor:v 
			{: RESULT = l; RESULT.addElem(v); :}
		;
valor ::= bool:b {: RESULT = b; :}
		| num:n {: RESULT = n; :}
		| str:s {: RESULT = s; :}
		| obj:o {: RESULT = o; :}
		| PESOS {: RESULT = parser.getExpresion().getNodo(); :}
		;
		
expresion ::= valor:v {: RESULT = new Expresion(v); parser.setExpresion(RESULT); :}
			| P1 expresion:e1 P2 {: RESULT = e1; parser.setExpresion(RESULT); :}	//No le gusta esta lï¿½nea con P1 y P2
			| expresion:e1 INTERSECCION expresion:e2 
				{: RESULT = new Interseccion(e1,e2); parser.setExpresion(RESULT); :}
			| expresion:e1 UNION expresion:e2 
				{: RESULT = new Union(e1,e2); parser.setExpresion(RESULT); :}
			| expresion:e1 CONCATENACION expresion:e2
				{: RESULT = new Concatenacion(e1,e2); parser.setExpresion(RESULT); :}
			| expresion:e1 DIFERENCIA expresion:e2
				{: RESULT = new Diferencia(e1,e2); parser.setExpresion(RESULT); :}
			| expresion:e id:i 
				{: RESULT = new Filtro(e,i); parser.setExpresion(RESULT); :}
			| expresion:e FILTRO id:i
				{: RESULT = new Regex(e,i); parser.setExpresion(RESULT); :}
			| expresion:e ATTRIBUTES 
				{: RESULT = new Attributes(e); parser.setExpresion(RESULT); :}
			| expresion:e ELEMENTS
				{: RESULT = new Elements(e); parser.setExpresion(RESULT); :}
			;